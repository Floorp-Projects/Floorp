/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* vim: set sw=2 ts=8 et tw=80 ft=cpp : */

/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PNecko;

include "mozilla/net/NeckoMessageUtils.h";

using IPC::URI;
using IPC::InputStream;
using PRTime;

namespace mozilla {
namespace net {

async protocol PFTPChannel
{
  manager PNecko;

parent:
  __delete__();

  AsyncOpen(URI uri,
            PRUint64 startPos,
            nsCString entityID,
            InputStream uploadStream,
            bool haveLoadContext,
            bool isContent,
            bool usePrivateBrowsing,
            bool isInBrowserElement,
            PRUint32 appID,
            nsCString extendedOrigin);
  ConnectChannel(PRUint32 channelId);
  Cancel(nsresult status);
  Suspend();
  Resume();

child:
  OnStartRequest(PRInt32 aContentLength, nsCString aContentType,
                 PRTime aLastModified, nsCString aEntityID, URI aURI);
  OnDataAvailable(nsCString data, PRUint32 offset, PRUint32 count);
  OnStopRequest(nsresult statusCode);
  FailedAsyncOpen(nsresult statusCode);
  DeleteSelf();
};

} // namespace net
} // namespace mozilla

