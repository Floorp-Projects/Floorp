/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* vim: set sw=2 ts=8 et tw=80 ft=cpp : */

/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

include protocol PBackground;
include NeckoChannelParams;

include "mozilla/net/NeckoMessageUtils.h";

using class nsHttpHeaderArray from "nsHttpHeaderArray.h";

namespace mozilla {
namespace net {

//-------------------------------------------------------------------
async refcounted protocol PHttpBackgroundChannel
{
  manager PBackground;

child:
  // OnStartRequest is sent over main thread IPC. The following
  // OnTransportAndData/OnStopRequest/OnProgress/OnStatus/FlushForDiversion/
  // DivertMessages needs to wait in content process until OnStartRequest
  // is processed. For synchronizing the event sequence.
  async OnStartRequestSent();

  // Combines a single OnDataAvailable and its associated OnProgress &
  // OnStatus calls into one IPDL message
  // This is duplicated on PHttpChannel, which gets used for multi-part
  // streams to make synchronization when we get OnStartRequest multiple
  // times easier.
  async OnTransportAndData(nsresult  channelStatus,
                           nsresult  transportStatus,
                           uint64_t  offset,
                           uint32_t  count,
                           nsCString data,
                           bool dataFromSocketProcess);

  // This is duplicated on PHttpChannel, which gets used for multi-part
  // streams to make synchronization when we get OnStartRequest multiple
  // times easier.
  async OnStopRequest(nsresult channelStatus,
                      ResourceTimingStructArgs timing,
                      TimeStamp lastActiveTabOptimization,
                      nsHttpHeaderArray responseTrailers,
                      ConsoleReportCollected[] consoleReport);

  // Parent has been suspended for diversion; no more events to be enqueued.
  async FlushedForDiversion();

  // Child should resume processing the ChannelEventQueue, i.e. diverting any
  // OnDataAvailable and OnStopRequest messages in the queue back to the parent.
  async DivertMessages();

  async __delete__();

};

} // namespace net
} // namespace mozilla
