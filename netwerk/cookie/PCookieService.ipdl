/* -*- Mode: C++; tab-width: 8; indent-tabs-mode: nil; c-basic-offset: 2 -*- */
/* vim: set sw=2 ts=8 et tw=80 ft=cpp : */

/* ***** BEGIN LICENSE BLOCK *****
 * Version: MPL 1.1/GPL 2.0/LGPL 2.1
 *
 * The contents of this file are subject to the Mozilla Public License Version
 * 1.1 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 * http://www.mozilla.org/MPL/
 *
 * Software distributed under the License is distributed on an "AS IS" basis,
 * WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
 * for the specific language governing rights and limitations under the
 * License.
 *
 * The Original Code is mozilla.org code.
 *
 * The Initial Developer of the Original Code is
 *  The Mozilla Foundation
 * Portions created by the Initial Developer are Copyright (C) 2010
 * the Initial Developer. All Rights Reserved.
 *
 * Contributor(s):
 *   Daniel Witte <dwitte@mozilla.com>
 *
 * Alternatively, the contents of this file may be used under the terms of
 * either the GNU General Public License Version 2 or later (the "GPL"), or
 * the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
 * in which case the provisions of the GPL or the LGPL are applicable instead
 * of those above. If you wish to allow use of your version of this file only
 * under the terms of either the GPL or the LGPL, and not to allow others to
 * use your version of this file under the terms of the MPL, indicate your
 * decision by deleting the provisions above and replace them with the notice
 * and other provisions required by the GPL or the LGPL. If you do not delete
 * the provisions above, a recipient may use your version of this file under
 * the terms of any one of the MPL, the GPL or the LGPL.
 *
 * ***** END LICENSE BLOCK ***** */

include protocol PNecko;

include "mozilla/net/NeckoMessageUtils.h";

using IPC::URI;

namespace mozilla {
namespace net {

/**
 * PCookieService
 *
 * Provides IPDL methods for setting and getting cookies. These are stored on
 * and managed by the parent; the child process goes through the parent for
 * all cookie operations. Lower-level programmatic operations (i.e. those
 * provided by the nsICookieManager and nsICookieManager2 interfaces) are not
 * currently implemented and requesting these interfaces in the child will fail.
 *
 * @see nsICookieService
 * @see nsICookiePermission
 */

sync protocol PCookieService
{
  manager PNecko;

parent:

  /*
   * Get the complete cookie string associated with the URI. This is a sync
   * call in order to avoid race conditions -- for instance, an HTTP response
   * on the parent and script access on the child.
   *
   * @param host
   *        Same as the 'aURI' argument to nsICookieService.getCookieString.
   * @param isForeign
   *        True if the the request is third party, for purposes of allowing
   *        access to cookies. This should be obtained from
   *        mozIThirdPartyUtil.isThirdPartyChannel. Third party requests may be
   *        rejected depending on user preferences; if those checks are
   *        disabled, this parameter is ignored.
   * @param fromHttp
   *        Whether the result is for an HTTP request header. This should be
   *        true for nsICookieService.getCookieStringFromHttp calls, false
   *        otherwise.
   *
   * @see nsICookieService.getCookieString
   * @see nsICookieService.getCookieStringFromHttp
   * @see mozIThirdPartyUtil.isThirdPartyChannel
   *
   * @return the resulting cookie string.
   */
  sync GetCookieString(URI host,
                       bool isForeign,
                       bool fromHttp)
       returns (nsCString result);

  /*
   * Set a cookie string.
   *
   * @param host
   *        Same as the 'aURI' argument to nsICookieService.setCookieString.
   * @param isForeign
   *        True if the the request is third party, for purposes of allowing
   *        access to cookies. This should be obtained from
   *        mozIThirdPartyUtil.isThirdPartyChannel. Third party requests may be
   *        rejected depending on user preferences; if those checks are
   *        disabled, this parameter is ignored.
   * @param cookieString
   *        Same as the 'aCookie' argument to nsICookieService.setCookieString.
   * @param serverTime
   *        Same as the 'aServerTime' argument to
   *        nsICookieService.setCookieStringFromHttp. If the string is empty or
   *        null (e.g. for non-HTTP requests), the current local time is used.
   * @param fromHttp
   *        Whether the result is for an HTTP request header. This should be
   *        true for nsICookieService.setCookieStringFromHttp calls, false
   *        otherwise.
   *
   * @see nsICookieService.setCookieString
   * @see nsICookieService.setCookieStringFromHttp
   * @see mozIThirdPartyUtil.isThirdPartyChannel
   */
  SetCookieString(URI host,
                  bool isForeign,
                  nsCString cookieString,
                  nsCString serverTime,
                  bool fromHttp);

  __delete__();
};

}
}

