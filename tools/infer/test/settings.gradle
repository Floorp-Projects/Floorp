// Check out root/settings.gradle for more information

rootProject.name = 'infer'

def topsrcdir = rootProject.projectDir.absolutePath + '/../../..'

def commandLine = ["${topsrcdir}/mach", "environment", "--format", "json", "--verbose"]
def proc = commandLine.execute(null, new File(topsrcdir))
def standardOutput = new ByteArrayOutputStream()
proc.consumeProcessOutput(standardOutput, standardOutput)
proc.waitFor()

if (proc.exitValue() != 0) {
    throw new GradleException("Process '${commandLine}' finished with non-zero exit value ${proc.exitValue()}:\n\n${standardOutput.toString()}")
}

import groovy.json.JsonSlurper
def slurper = new JsonSlurper()
def json = slurper.parseText(standardOutput.toString())

gradle.ext.mozconfig = json

include 'autotest'
project(':autotest').projectDir = new File("${json.topsrcdir}/tools/infer/test/autotest")
