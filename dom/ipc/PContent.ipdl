/* -*- Mode: C++; c-basic-offset: 4; indent-tabs-mode: nil; tab-width: 8 -*- */
/* vim: set sw=4 ts=8 et tw=80 ft=cpp : */
/* ***** BEGIN LICENSE BLOCK *****
 * Version: MPL 1.1/GPL 2.0/LGPL 2.1
 *
 * The contents of this file are subject to the Mozilla Public License Version
 * 1.1 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 * http://www.mozilla.org/MPL/
 *
 * Software distributed under the License is distributed on an "AS IS" basis,
 * WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
 * for the specific language governing rights and limitations under the
 * License.
 *
 * The Original Code is Mozilla Content App.
 *
 * The Initial Developer of the Original Code is
 *   The Mozilla Foundation.
 * Portions created by the Initial Developer are Copyright (C) 2009
 * the Initial Developer. All Rights Reserved.
 *
 * Contributor(s):
 *
 * Alternatively, the contents of this file may be used under the terms of
 * either the GNU General Public License Version 2 or later (the "GPL"), or
 * the GNU Lesser General Public License Version 2.1 or later (the "LGPL"),
 * in which case the provisions of the GPL or the LGPL are applicable instead
 * of those above. If you wish to allow use of your version of this file only
 * under the terms of either the GPL or the LGPL, and not to allow others to
 * use your version of this file under the terms of the MPL, indicate your
 * decision by deleting the provisions above and replace them with the notice
 * and other provisions required by the GPL or the LGPL. If you do not delete
 * the provisions above, a recipient may use your version of this file under
 * the terms of any one of the MPL, the GPL or the LGPL.
 *
 * ***** END LICENSE BLOCK ***** */

include protocol PBrowser;
include protocol PTestShell;
include protocol PNecko;
include protocol PExternalHelperApp;

include "mozilla/chrome/RegistryMessageUtils.h";
include "mozilla/net/NeckoMessageUtils.h";

include "nsGeoPositionIPCSerialiser.h";

using GeoPosition;

using ChromePackage;
using ResourceMapping;
using OverrideMapping;
using IPC::URI;

namespace mozilla {
namespace dom {

rpc protocol PContent
{
    manages PBrowser;
    manages PTestShell;
    manages PNecko;
    manages PExternalHelperApp;

child:
    PBrowser(PRUint32 chromeFlags);

    PTestShell();

    RegisterChrome(ChromePackage[] packages, ResourceMapping[] resources,
                   OverrideMapping[] overrides);

    async SetOffline(PRBool offline);

    async NotifyVisited(URI uri);

    PreferenceUpdate(nsCString pref);

    NotifyAlertsObserver(nsCString topic, nsString data);

    GeolocationUpdate(GeoPosition somewhere);

parent:
    PNecko();

    // Services remoting

    async StartVisitedQuery(URI uri);
    async VisitURI(URI uri, URI referrer, PRUint32 flags);
    async SetURITitle(URI uri, nsString title);

    async LoadURIExternal(URI uri);

    // PrefService messages
    sync ReadPrefs() returns (nsCString retValue);

    // PermissionsManager messages
    sync TestPermission(URI uri, nsCString type, PRBool exact) returns (PRUint32 retValue);

    sync SyncMessage(nsString aMessage, nsString aJSON)
      returns (nsString[] retval);

    ShowAlertNotification(nsString imageUrl, 
                          nsString title, 
                          nsString text, 
                          PRBool textClickable,
                          nsString cookie,
                          nsString name);

    PExternalHelperApp(URI uri, nsCString aMimeContentType,
                       nsCString aContentDisposition, bool aForceSave,
                       PRInt64 aContentLength);
    
    GeolocationStart();
    GeolocationStop();

    ConsoleMessage(nsString message);
    ScriptError(nsString message, nsString sourceName, nsString sourceLine,
                PRUint32 lineNumber, PRUint32 colNumber, PRUint32 flags,
                nsCString category); 

both:
     AsyncMessage(nsString aMessage, nsString aJSON);

};

}
}
