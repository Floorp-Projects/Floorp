apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        targetSdkVersion 22
        minSdkVersion 9
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }

    sourceSets {
        main {
            java {
                exclude 'org/mozilla/gecko/resources/**'

                if (!mozconfig.substs.MOZ_CRASHREPORTER) {
                    exclude 'org/mozilla/gecko/CrashReporter.java'
                }

                if (!mozconfig.substs.MOZ_NATIVE_DEVICES) {
                    exclude 'org/mozilla/gecko/ChromeCast.java'
                    exclude 'org/mozilla/gecko/GeckoMediaPlayer.java'
                    exclude 'org/mozilla/gecko/MediaPlayerManager.java'
                }

                if (mozconfig.substs.MOZ_WEBRTC) {
                    srcDir 'src/webrtc_audio_device'
                    srcDir 'src/webrtc_video_capture'
                    srcDir 'src/webrtc_video_render'
                }

                // Adjust helpers are included in the preprocessed_code project.
                exclude 'org/mozilla/gecko/adjust/**'
            }

            res {
                if (mozconfig.substs.MOZ_CRASHREPORTER) {
                    srcDir "src/crashreporter/res"
                }
            }
        }

        test {
            java {
                srcDir "src/background_junit4"
            }

            resources {
                srcDir "resources/background_junit4"
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.0.1'
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:recyclerview-v7:23.0.1'

    if (mozconfig.substs.MOZ_NATIVE_DEVICES) {
        compile 'com.android.support:mediarouter-v7:22.2.1'
        compile 'com.google.android.gms:play-services-basement:8.1.0'
        compile 'com.google.android.gms:play-services-base:8.1.0'
        compile 'com.google.android.gms:play-services-cast:8.1.0'
    }

    compile project(':branding')
    compile project(':preprocessed_code')
    compile project(':preprocessed_resources')
    compile project(':thirdparty')

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.simpleframework:simple-http:4.1.13'
}

apply plugin: 'idea'

idea {
    module {
        excludeDirs += file('org/mozilla/gecko/resources')
    }
}
