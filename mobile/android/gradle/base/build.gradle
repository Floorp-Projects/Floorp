apply plugin: 'com.android.library'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        targetSdkVersion 21
        minSdkVersion 9
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        incremental true
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }

    sourceSets {
        main {
            java {
                exclude 'org/mozilla/gecko/tests/**'
                exclude 'org/mozilla/gecko/resources/**'
                if (!mozconfig.substs.MOZ_CRASHREPORTER) {
                    exclude 'org/mozilla/gecko/CrashReporter.java'
                }
                // Adjust helpers are included in the preprocessed_code project.
                exclude 'org/mozilla/gecko/adjust/**'
            }

            res {
                if (mozconfig.substs.MOZ_CRASHREPORTER) {
                    srcDir "src/crashreporter/res"
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:21.+'

    if (mozconfig.substs.MOZ_NATIVE_DEVICES) {
        compile 'com.android.support:appcompat-v7:21.+'
        compile 'com.android.support:mediarouter-v7:21.+'
        compile 'com.google.android.gms:play-services-base:6.5.+'
        compile 'com.google.android.gms:play-services-cast:6.5.+'
    }

    compile project(':branding')
    compile project(':preprocessed_code')
    compile project(':preprocessed_resources')
    compile project(':thirdparty')
}

android.libraryVariants.all { variant ->
    variant.checkManifest.dependsOn generateCodeAndResources
}

apply plugin: 'idea'

idea {
    module {
        excludeDirs += file('org/mozilla/gecko/resources')
        excludeDirs += file('org/mozilla/gecko/tests')
    }
}
