apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        targetSdkVersion 22
        minSdkVersion 9
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java {
                srcDir "${project.buildDir}/generated/source/java"

                srcDir 'src/adjust/java'
                if (mozconfig.substs.MOZ_INSTALL_TRACKING) {
                    exclude 'org/mozilla/gecko/adjust/StubAdjustHelper.java'
                } else {
                    exclude 'org/mozilla/gecko/adjust/AdjustHelper.java'
                }
            }
        }
    }
}

task syncGeneratedSources(type: Sync) {
    into("${project.buildDir}/generated/source/java")
    from("${topobjdir}/mobile/android/base/generated/preprocessed")
}

android.libraryVariants.all { variant ->
    // variant does not expose its generate sources task.
    def name = variant.buildType.name
    def generateSourcesTask = tasks.findByName("generate${name.capitalize()}Sources")
    generateSourcesTask.dependsOn syncGeneratedSources
}

dependencies {
    if (mozconfig.substs.MOZ_INSTALL_TRACKING) {
        compile project(':thirdparty_adjust_sdk')
    }
}
