buildDir "${topobjdir}/gradle/build/mobile/android/geckoview_example"

apply plugin: 'com.android.application'

apply from: "${topsrcdir}/mobile/android/gradle/product_flavors.gradle"

android {
    buildToolsVersion project.ext.buildToolsVersion
    compileSdkVersion project.ext.compileSdkVersion

    defaultConfig {
        targetSdkVersion project.ext.targetSdkVersion
        minSdkVersion project.ext.minSdkVersion
        manifestPlaceholders = project.ext.manifestPlaceholders

        applicationId "org.mozilla.geckoview_example"
        versionCode project.ext.versionCode
        versionName project.ext.versionName

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // By default the android plugins ignores folders that start with `_`, but
    // we need those in web extensions.
    // See also:
    //  - https://issuetracker.google.com/issues/36911326
    //  - https://stackoverflow.com/questions/9206117/how-to-workaround-autoomitting-fiiles-folders-starting-with-underscore-in
    aaptOptions {
        ignoreAssetsPattern  '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        noCompress 'ja'
    }

    project.configureProductFlavors.delegate = it
    project.configureProductFlavors()

    buildFeatures {
        buildConfig true
    }

    namespace 'org.mozilla.geckoview_example'
}

dependencies {
    implementation "androidx.annotation:annotation:1.6.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.preference:preference:1.1.1"

    implementation project(path: ':geckoview')
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'

    implementation 'androidx.multidex:multidex:2.0.1'
}
