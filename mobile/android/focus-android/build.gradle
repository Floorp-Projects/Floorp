// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.architecture_components_version = '2.0.0-beta01'
    ext.support_libraries_version = '1.0.0-beta01'
    ext.espresso_version = '3.1.0-alpha4'
    ext.kotlin_version = '1.3.0'
    ext.coroutines_version = '1.0.1'
    ext.spotbugs_version = '3.1.4'
    ext.mozilla_components_version = '8.0.0'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.3.1.1'
        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.1"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    // The version number is duplicated in the detekt block. We are unable to use constants due to
    // limitations of the gradle plugin. We've decided to stick with duplication for now since the
    // other methods involve using undocumented APIs
    // https://docs.gradle.org/current/userguide/plugins.html#plugins_dsl_limitations
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
    id "org.jetbrains.kotlin.kapt" version "1.2.50"
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.0.0.RC6-3"
    profile("main") {
        input = "$projectDir"
        config = "$projectDir/quality/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/app/build/reports/detekt"
    }
}

allprojects {
    repositories {
        google()
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.21.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
