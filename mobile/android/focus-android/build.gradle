// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.architecture_components_version = '2.1.0'
    ext.support_libraries_version = '1.0.0'
    ext.espresso_version = '3.1.0-alpha4'
    ext.kotlin_version = '1.3.61'
    ext.coroutines_version = '1.3.0'
    ext.mozilla_components_version = '72.0.15'
    // Pinning the last working version of the service-telemetry component until we decide
    // what we want to do with telemetry in the app.
    ext.mozilla_components_version_telemetry = '57.0.9'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath 'de.mannodermaus.gradle.plugins:android-junit5:1.4.2.1'
        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    // The version number is duplicated in the detekt block. We are unable to use constants due to
    // limitations of the gradle plugin. We've decided to stick with duplication for now since the
    // other methods involve using undocumented APIs
    // https://docs.gradle.org/current/userguide/plugins.html#plugins_dsl_limitations
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC6-3"
    id "org.jetbrains.kotlin.kapt" version "1.2.50"
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.0.0.RC6-3"
    profile("main") {
        input = "$projectDir"
        config = "$projectDir/quality/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/app/build/reports/detekt"
    }
}

allprojects {
    repositories {
        google()
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
