// Top-level build file where you can add configuration options common to all sub-projects/modules.

import org.mozilla.focus.gradle.tasks.GithubDetailsTask

buildscript {
    repositories {
        google()
        mavenCentral()
        maven {
            // We will always need this repository for retrieving stable dependencies (like Glean).
            url "https://maven.mozilla.org/maven2"
        }
        maven {
            url "https://nightly.maven.mozilla.org/maven2"
        }
    }

    dependencies {
        classpath FocusDependencies.android_gradle_plugin
        classpath FocusDependencies.kotlin_gradle_plugin
        classpath "org.mozilla.telemetry:glean-gradle-plugin:${Versions.mozilla_glean}"
        classpath "org.mozilla.appservices:tooling-nimbus-gradle:${Versions.mozilla_appservices}"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.19.0" // Variables in plugins {} aren't supported
}

detekt {
    buildUponDefaultConfig = true
    input = files("$projectDir/app")
    config = files("$projectDir/quality/detekt.yml")
    baseline = file("$projectDir/quality/detekt-baseline.xml")

    reports {
        html {
            enabled = true
            destination = file("$projectDir/build/reports/detekt.html")
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://nightly.maven.mozilla.org/maven2"
        }
        maven {
            url "https://maven.mozilla.org/maven2"
        }
    }
}

subprojects {
    // Define a reusable task for updating the version in manifest.json for modules that package
    // a web extension. We automate this to make sure we never forget to update the version, either
    // in local development or for releases. In both cases, we want to make sure the latest version
    // of all extensions (including their latest changes) are installed on first start-up.
    ext.updateExtensionVersion = { task, extDir ->
        configure(task) {
            from extDir
            include 'manifest.template.json'
            rename { 'manifest.json' }
            into extDir

            def values = ['version': rootProject.ext.config.componentsVersion.split("a|b")[0] + "." + new Date().format('MMddHHmmss')]
            inputs.properties(values)
            expand(values)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint("com.pinterest:ktlint:${FocusVersions.ktlint_version}") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "app/**/*.kt", "!**/build/**/*.kt", "buildSrc/**/*.kt"
}


task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "app/**/*.kt", "!**/build/**/*.kt", "buildSrc/**/*.kt"
}

tasks.register("listRepositories") {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
   }
}

tasks.register("githubTestDetails", GithubDetailsTask) {
    text = "### [Unit Test Results]({reportsUrl}/test/testFocusDebugUnitTest/index.html)"
}

tasks.register("githubLintDetektDetails", GithubDetailsTask) {
    text = "### [Detekt Results Focus]({reportsUrl}/detekt.html)"
}

tasks.register("githubLintAndroidDetails", GithubDetailsTask) {
    text = "### [Android Lint Results Focus]({reportsUrl}/lint-results-debug.html)"
}
