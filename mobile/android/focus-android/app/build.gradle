apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'jacoco-android'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'kotlin-android-extensions'
apply from: "$project.rootDir/tools/gradle/versionCode.gradle"

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "org.mozilla"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 11 // This versionCode is "frozen" for local builds. For "release" builds we
                       // override this with a generated versionCode at build time.
        versionName "4.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    dexOptions {
        preDexLibraries true
    }

    // We have a three dimensional build configuration:
    // BUILD TYPE (debug, beta, release) X PRODUCT FLAVOR (focus, klar) X ENGINE FLAVOR (webview, gecko)

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        beta {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".beta"
        }
        debug {
            applicationIdSuffix ".debug"
        }
        // Use a separate buildType for coverage: testCoverageEnabled produces slower code (4-5x slower
        // in places that I've benchmarked), and more importantly seems to break debugging with Android Studio
        // for some developers (i.e. variables can't be inspected or seen).
        coverage {
            initWith debug

            applicationIdSuffix ".coverage"
            testCoverageEnabled true
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    flavorDimensions "product", "engine", "abi"

    productFlavors {
        // In most countries we are Firefox Focus - but in some we need to be Firefox Klar
        focus {
            dimension "product"

            applicationIdSuffix ".focus"
        }
        klar {
            dimension "product"

            applicationIdSuffix ".klar"
        }

        // We can build with two engines: webview or gecko
        webview {
            dimension "engine"
        }

        gecko {
            dimension "engine"

            applicationIdSuffix ".gecko"
        }

        x86 {
            dimension "abi"
        }

        arm {
            dimension "abi"
        }

        universal {
            dimension "abi"
        }
    }

    variantFilter { variant ->
        def flavors = variant.flavors*.name
        // We only need a gecko debug and beta build for now.
        if (flavors.contains("gecko") && flavors.contains("focus")) {
            setIgnore(true)
        }

        if (flavors.contains("webview") && !flavors.contains("universal")) {
            // WebView builds support all ABIs
            setIgnore(true)
        }

        if (flavors.contains("gecko") && flavors.contains("universal")) {
            // There are only x86 and ARM builds using Gecko
            setIgnore(true)
        }
    }

    sourceSets {
        test {
            resources {
                // Make the default asset folder available as test resource folder. Robolectric seems
                // to fail to read assets for our setup. With this we can just read the files directly
                // and do not need to rely on Robolectric.
                srcDir "${projectDir}/src/main/assets/"
            }
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // WebView
        ////////////////////////////////////////////////////////////////////////////////////////////

        // Release

        focusWebviewUniversalRelease {
            java.srcDir 'src/focusRelease/java'
            manifest.srcFile 'src/focusRelease/AndroidManifest.xml'
            res.srcDir 'src/focusRelease/res'
        }

        klarWebviewUniversalRelease {
            java.srcDir 'src/klarRelease/java'
            res.srcDir 'src/klarRelease/res'
        }

        // Beta

        focusWebviewUniversalBeta {
            res.srcDir 'src/focusBeta/res'
        }

        klarWebviewUniversalBeta {
            res.srcDir 'src/klarBeta/res'
        }

        // Debug

        focusWebviewDebug {
            res.srcDir 'src/focusDebug/res'
        }

        klarWebviewDebug {
            res.srcDir 'src/klarDebug/res'
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // Gecko
        ////////////////////////////////////////////////////////////////////////////////////////////

        // Release

        klarGeckoArmRelease {
            java.srcDir 'src/klarRelease/java'
            res.srcDir 'src/klarRelease/res'
        }

        klarGeckoX86Release {
            java.srcDir 'src/klarRelease/java'
            res.srcDir 'src/klarRelease/res'
        }

        focusGeckoArmRelease {
            java.srcDir 'src/focusRelease/java'
            manifest.srcFile 'src/focusRelease/AndroidManifest.xml'
            res.srcDir 'src/focusRelease/res'
        }

        focusGeckoX86Release {
            java.srcDir 'src/focusRelease/java'
            manifest.srcFile 'src/focusRelease/AndroidManifest.xml'
            res.srcDir 'src/focusRelease/res'
        }

        // Beta

        focusGeckoArmBeta {
            res.srcDir 'src/focusBeta/res'
        }

        focusGeckoX86Beta {
            res.srcDir 'src/focusBeta/res'
        }

        klarGeckoArmBeta {
            res.srcDir 'src/klarBeta/res'
        }

        klarGeckoX86Beta {
            res.srcDir 'src/klarBeta/res'
        }

        // Debug

        focusGeckoArmDebug {
            res.srcDir 'src/focusDebug/res'
        }

        focusGeckoX86Debug {
            res.srcDir 'src/focusDebug/res'
        }

        klarGeckoArmDebug {
            res.srcDir 'src/klarDebug/res'
        }

        klarGeckoX86Debug {
            res.srcDir 'src/klarDebug/res'
        }
    }
}

kotlin {
    experimental { coroutines 'enable' }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}

repositories {
    mavenCentral()
    maven {
        // For the latest version of GeckoView (moving target!) use:
        //   https://index.taskcluster.net/v1/task/gecko.v2.mozilla-central.nightly.latest.mobile.android-api-16-opt/artifacts/public/android/maven
        //
        // For discovering available versions go to:
        //   https://tools.taskcluster.net/index/gecko.v2.mozilla-central.nightly

        // ARM GeckoView builds
        url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-central.nightly.2018.02.28.latest.mobile.android-api-16-opt/artifacts/public/android/maven"
    }
    maven {
        // x86 GeckoView builds
        url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-central.nightly.2018.02.28.latest.mobile.android-x86-opt/artifacts/public/android/maven"
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:$support_libraries_version"
    compile "com.android.support:customtabs:$support_libraries_version"
    compile "com.android.support:design:$support_libraries_version"
    compile "com.android.support:cardview-v7:$support_libraries_version"
    compile "com.android.support:recyclerview-v7:$support_libraries_version"

    compile "android.arch.lifecycle:extensions:$architecture_components_version"

    compile ("com.google.code.findbugs:annotations:3.0.1") {
        // We really only need the SuppressFBWarnings annotation, everything else can be ignored.
        // Without this we get weird failures due to dependencies.
        transitive = false
    }

    compile 'org.mozilla.telemetry:telemetry:1.1.1'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.19.3"

    focusCompile 'com.adjust.sdk:adjust-android:4.11.4'
    focusCompile 'com.google.android.gms:play-services-analytics:10.2.6' // Required by Adjust

    // What I actually want to define here is geckoArmCompile and geckoX86Compile. But gradle doesn't
    // allow me to define dependencies for such a flavor combination. However as we are not building
    // WebView + (ARM / x86) flavor combinations this should be good enough for now.
    armCompile "org.mozilla:geckoview-nightly-armeabi-v7a:60.0a1"
    x86Compile "org.mozilla:geckoview-nightly-x86:60.0a1"

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.7.1"
    testCompile 'org.mockito:mockito-core:2.12.0'

    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'

    androidTestCompile "com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestCompile "com.android.support.test.espresso:espresso-web:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile 'com.squareup.okhttp3:mockwebserver:3.7.0'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.7.0'

    androidTestCompile "tools.fastlane:screengrab:1.1.0", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

// -------------------------------------------------------------------------------------------------
//  Dynamically set versionCode (See tools/build/versionCode.gradle
// -------------------------------------------------------------------------------------------------

android.applicationVariants.all { variant ->
    def buildType = variant.buildType.name
    def isBetaOrRelease = buildType == "release" || buildType == "beta"

    if (isBetaOrRelease) {
        def versionCode = generatedVersionCode

        // The Google Play Store does not allow multiple APKs for the same app that all have the
        // same version code. Therefore we need to have different version codes for our ARM and x86
        // builds.
        // Our generated version code has a length of 8 (See tools/gradle/versionCode.gradle).
        // We will prefix our ARM builds with 1 and our x86 builds with 2. Our x86 builds need a
        // higher version code to avoid installing ARM builds on an x86 device with ARM
        // compatibility mode.
        def multiplier = 100000000

        if (variant.flavorName.contains("X86")) {
            versionCode = versionCode + (2 * multiplier)
        } else if (variant.flavorName.contains("Arm")) {
            versionCode = versionCode + (1 * multiplier)
        }

        variant.mergedFlavor.versionCode = versionCode
    }

    println("----------------------------------------------")
    println("Build type:   " + buildType)
    println("Flavor:       " + variant.flavorName)
    println("Version code: " + variant.mergedFlavor.versionCode)


}

// -------------------------------------------------------------------------------------------------
// Generate blocklists
// -------------------------------------------------------------------------------------------------

def blockListOutputDir = 'src/webview/res/raw'

task buildBlocklists(type:Copy) {
    from('../shavar-prod-lists') {
        include '*.json'
    }
    into blockListOutputDir

    // Android can't handle dashes in the filename, so we need to rename:
    rename 'disconnect-blacklist.json', 'blocklist.json'
    rename 'disconnect-entitylist.json', 'entitylist.json'
    // google_mapping.json already has an expected name
}

clean.doLast {
    file(blockListOutputDir).deleteDir()
}

tasks.whenTaskAdded { task ->
    def name = task.name
    if (name.contains("generate") && name.contains("Config") && name.contains("Webview")) {
        task.dependsOn buildBlocklists
    }
}

// -------------------------------------------------------------------------------------------------
// Adjust: Read token from locale file if it exists (Only release builds)
// -------------------------------------------------------------------------------------------------

android.applicationVariants.all { variant ->
    def variantName = variant.getName()

    print("Adjust token: ")

    if (variantName.contains("Release") && variantName.contains("focus")) {
        try {
            def token = new File("${rootDir}/.adjust_token").text.trim()
            buildConfigField 'String', 'ADJUST_TOKEN', '"' + token + '"'
            println "(Added from .adjust_token file)"
        } catch (java.io.FileNotFoundException e) {
            buildConfigField 'String', 'ADJUST_TOKEN', 'null'
            println("X_X")
        }
    } else {
        buildConfigField 'String', 'ADJUST_TOKEN', 'null'
        println("--")
    }
}

// -------------------------------------------------------------------------------------------------
// L10N: Generate list of locales
// Focus provides its own (Android independent) locale switcher. That switcher requires a list
// of locale codes. We generate that list here to avoid having to manually maintain a list of locales:
// -------------------------------------------------------------------------------------------------

def getEnabledLocales() {
    def resDir = file('src/main/res')

    def potentialLanguageDirs = resDir.listFiles(new FilenameFilter() {
        @Override
        boolean accept(File dir, String name) {
            return name.startsWith("values-");
        }
    })

    def langs = potentialLanguageDirs.findAll {
        // Only select locales where strings.xml exists
        // Some locales might only contain e.g. sumo URLS in urls.xml, and should be skipped (see es vs es-ES/es-MX/etc)
        return  file(new File(it, "strings.xml")).exists()
    } .collect {
        // And reduce down to actual values-* names
        return it.name
    } .collect {
        return it.substring("values-".length())
    } .collect {
        if (it.length() > 3 && it.contains("-r")) {
            // Android resource dirs add an "r" prefix to the region - we need to strip that for java usage
            // Add 1 to have the index of the r, without the dash
            def regionPrefixPosition = it.indexOf("-r") + 1

            return it.substring(0, regionPrefixPosition) + it.substring(regionPrefixPosition + 1)
        } else {
            return it
        }
    }.collect {
        return '"' + it + '"'
    }

    // en-US is the default language (in "values") and therefore needs to be added separately
    langs << "\"en-US\""

    return langs.sort { it }
}

def generatedLocaleListDir = 'src/main/java/org/mozilla/focus/generated'
def generatedLocaleListFilename = 'LocaleList.java'

task generateLocaleList {
    doLast {
        def dir = file(generatedLocaleListDir)
        dir.mkdir()
        def localeList = file(new File(dir, generatedLocaleListFilename))

        localeList.delete()
        localeList.createNewFile()
        localeList << "package org.mozilla.focus.generated;" << "\n" << "\n"
        localeList << "import java.util.Arrays;" << "\n"
        localeList << "import java.util.Collections;" << "\n"
        localeList << "import java.util.List;" << "\n"
        localeList << "\n"
        localeList << "public class LocaleList {" << "\n"
        // findbugs doesn't like "public static final String[]", see http://findbugs.sourceforge.net/bugDescriptions.html#MS_MUTABLE_ARRAY
        localeList << "    public static final List<String> BUNDLED_LOCALES = Collections.unmodifiableList(Arrays.asList(new String[] { "
        localeList << getEnabledLocales().join(", ") + " }));" << "\n"
        localeList << "}" << "\n"
    }
}

tasks.whenTaskAdded { task ->
    if (name.contains("compile")) {
        task.dependsOn generateLocaleList
    }
}

clean.doLast {
    file(generatedLocaleListDir).deleteDir()
}

// -------------------------------------------------------------------------------------------------
// Static Analysis: findbugs and pmd
// -------------------------------------------------------------------------------------------------

findbugs {
    ignoreFailures = false

    effort = "max"
    // This selects what level of bugs to report: low means low priority issues will be reported
    // (in addition to medium+high), which corresponds to warning about everything.
    // TODO: boost this to low once low priority issues are fixed.
    reportLevel = "medium"

    excludeFilter = new File("${project.rootDir}/quality/findbugs-exclude.xml")
}

task findbugs(type: FindBugs, dependsOn: "assemble", group: 'verification') {
    classes = files("$projectDir.absolutePath/build/intermediates/classes")

    source = fileTree('src/main/java')
    classpath = files()

    // Only one report format is supported. Html is easier to read, so let's use that
    // (xml is the one that's enabled by default).
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    toolVersion = '5.5.2'
    ignoreFailures = true
    ruleSetFiles = files("${project.rootDir}/quality/pmd-rules.xml")
    ruleSets = []
}

task pmd(type: Pmd, group: 'verification') {
    source 'src'
    include '**/*.java'
    exclude('**/gen/**',
            '**/debug/**',
            '**/androidTest/**',
            '**/test**/**')

    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination "$projectDir.absolutePath/build/reports/pmd/pmd.html"
        }
    }
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/quality/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

afterEvaluate {
    check.dependsOn 'findbugs', 'pmd', 'checkstyle'
}