import com.github.spotbugs.SpotBugsTask

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "com.github.spotbugs"
apply plugin: 'jacoco'
apply plugin: 'jacoco-android'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: 'kotlin-android-extensions'
apply from: "$project.rootDir/tools/gradle/versionCode.gradle"

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "org.mozilla"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 11 // This versionCode is "frozen" for local builds. For "release" builds we
                       // override this with a generated versionCode at build time.
        versionName "6.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    dexOptions {
        preDexLibraries true
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    // We have a three dimensional build configuration:
    // BUILD TYPE (debug, release) X PRODUCT FLAVOR (focus, klar) X ENGINE FLAVOR (webview, gecko)

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    flavorDimensions "product", "engine", "abi"

    productFlavors {
        // In most countries we are Firefox Focus - but in some we need to be Firefox Klar
        focus {
            dimension "product"

            applicationIdSuffix ".focus"
        }
        klar {
            dimension "product"

            applicationIdSuffix ".klar"
        }

        // We can build with two engines: webview or gecko
        webview {
            dimension "engine"
        }

        gecko {
            dimension "engine"
        }

        x86 {
            dimension "abi"
        }

        arm {
            dimension "abi"
        }

        aarch64 {
            dimension "abi"
        }
    }

    sourceSets {
        test {
            resources {
                // Make the default asset folder available as test resource folder. Robolectric seems
                // to fail to read assets for our setup. With this we can just read the files directly
                // and do not need to rely on Robolectric.
                srcDir "${projectDir}/src/main/assets/"
            }
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // WebView
        ////////////////////////////////////////////////////////////////////////////////////////////

        // Release
        focusWebviewX86Release.root = 'src/focusRelease'
        focusWebviewArmRelease.root = 'src/focusRelease'
        focusWebviewAarch64Release.root = 'src/focusRelease'

        klarWebviewX86Release.root = 'src/klarRelease'
        klarWebviewArmRelease.root = 'src/klarRelease'
        klarWebviewAarch64Release.root = 'src/klarRelease'

        // Debug
        focusWebviewX86Debug.root = 'src/focusDebug'
        focusWebviewX86Debug.root = 'src/focusDebug'
        focusWebviewX86Debug.root = 'src/focusDebug'

        klarWebviewX86Debug.root = 'src/klarDebug'
        klarWebviewX86Debug.root = 'src/klarDebug'
        klarWebviewX86Debug.root = 'src/klarDebug'

        // UI test
        androidTestWebviewCompile {
            java.srcDirs += ['src/androidTestWebview/java']
        }


        ////////////////////////////////////////////////////////////////////////////////////////////
        // Gecko
        ////////////////////////////////////////////////////////////////////////////////////////////

        // Release

        klarGeckoArmRelease.root = 'src/klarRelease'
        klarGeckoX86Release.root = 'src/klarRelease'
        klarGeckoAarch64Release.root = 'src/klarRelease'

        focusGeckoArmRelease.root = 'src/focusRelease'
        focusGeckoX86Release.root = 'src/focusRelease'
        focusGeckoAarch64Release.root = 'src/focusRelease'

        // Debug

        focusGeckoArmDebug.root = 'src/focusDebug'
        focusGeckoX86Debug.root = 'src/focusDebug'
        focusGeckoAarch64Debug.root = 'src/focusDebug'

        klarGeckoArmDebug.root = 'src/klarDebug'
        klarGeckoX86Debug.root = 'src/klarDebug'
        klarGeckoAarch64Debug.root = 'src/klarDebug'

        // UI tests
        androidTestGeckoCompile {
           java.srcDirs += ['src/androidTestGecko/java']
        }
    }
}

kotlin {
    experimental { coroutines 'enable' }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
}

repositories {
    mavenCentral()
    maven {
        //  For the latest version of GeckoView (moving target!):
        //
        //  Look at the tags on the mozilla-beta repository:
        //  hg.mozilla.org/releases/mozilla-beta/tags
        //
        //  Find the latest beta release tag. They look like this: FENNEC_61_0b13_RELEASE
        //  (With Fennec we build GeckoView too; This tag is for 61.0 Beta 13).
        //
        //  Click on the changeset ID to find the matching changeset/commit ID (shortened).
        //  For the example tag above that's 5b5e620e33ee.
        //
        //  Now go to the list of revisions to find the matching changeset
        //  (It should start with the ID you got from the tag, remember the full ID!):
        //  tools.taskcluster.net/index/gecko.v2.mozilla-beta.revision
        //
        //  Click through until you find the artifacts (revision -> mobile -> android-api-16-opt).
        //
        //  Remember the version of the artifacts. In the example above that's: 61.0.20180611134439

        // ARM GeckoView builds
        url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-beta.revision" +
                ".${geckoview_revision}.mobile.android-api-16-opt/artifacts/public/android/maven"
    }
    maven {
        // x86 GeckoView builds
        url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-beta.revision" +
                ".${geckoview_revision}.mobile.android-x86-opt/artifacts/public/android/maven"
    }
    maven {
        // aarch64 builds
        url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-beta.revision" +
                ".${geckoview_revision}.mobile.android-aarch64-opt/artifacts/public/android/maven"
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$support_libraries_version"
    implementation "com.android.support:customtabs:$support_libraries_version"
    implementation "com.android.support:design:$support_libraries_version"
    implementation "com.android.support:cardview-v7:$support_libraries_version"
    implementation "com.android.support:recyclerview-v7:$support_libraries_version"
    implementation 'io.sentry:sentry-android:1.7.3'

    implementation "android.arch.lifecycle:extensions:$architecture_components_version"

    compileOnly 'net.jcip:jcip-annotations:1.0'
    compileOnly "com.github.spotbugs:spotbugs-annotations:$spotbugs_version"

    implementation "org.mozilla.components:telemetry:$mozilla_components_version"
    implementation "org.mozilla.components:ktx:$mozilla_components_version"
    implementation "org.mozilla.components:utils:$mozilla_components_version"
    implementation "org.mozilla.components:domains:$mozilla_components_version"
    implementation "org.mozilla.components:autocomplete:$mozilla_components_version"
    implementation "org.mozilla.components:search:$mozilla_components_version"
    implementation "org.mozilla.photon:colors:$mozilla_components_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5"

    focusImplementation 'com.adjust.sdk:adjust-android:4.11.4'
    focusImplementation 'com.google.android.gms:play-services-analytics:10.2.6' // Required by Adjust

    armImplementation "org.mozilla:geckoview-beta-armeabi-v7a:${geckoview_version}"
    x86Implementation "org.mozilla:geckoview-beta-x86:${geckoview_version}"
    aarch64Implementation "org.mozilla:geckoview-beta-arm64-v8a:${geckoview_version}"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.8"
    testImplementation 'org.mockito:mockito-core:2.12.0'

    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'

    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:$espresso_version", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.7.0'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.7.0'

    androidTestImplementation "tools.fastlane:screengrab:1.1.0", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    implementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.android.support.test:rules:1.0.2'

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.jakewharton:process-phoenix:2.0.0'
    debugImplementation 'com.facebook.stetho:stetho:1.5.0'
}
// -------------------------------------------------------------------------------------------------
//  Dynamically set versionCode (See tools/build/versionCode.gradle
// -------------------------------------------------------------------------------------------------

android.applicationVariants.all { variant ->
    def buildType = variant.buildType.name

    if (buildType == "release") {
        def versionCode = generatedVersionCode

        // The Google Play Store does not allow multiple APKs for the same app that all have the
        // same version code. Therefore we need to have different version codes for our ARM and x86
        // builds.
        // Our generated version code has a length of 8 (See tools/gradle/versionCode.gradle).
        // We will prefix our ARM builds with 1 and our x86 builds with 2. Our x86 builds need a
        // higher version code to avoid installing ARM builds on an x86 device with ARM
        // compatibility mode.
        def multiplier = 100000000

        if (variant.flavorName.contains("X86")) {
            versionCode = versionCode + (3 * multiplier)
        } else if (variant.flavorName.contains("Aarch64")) {
            versionCode = versionCode + (2 * multiplier)
        } else if (variant.flavorName.contains("Arm")) {
            versionCode = versionCode + (1 * multiplier)
        }

        variant.outputs.all { output ->
            setVersionCodeOverride(versionCode)
        }
    }

    println("----------------------------------------------")
    println("Build type:   " + buildType)
    println("Flavor:       " + variant.flavorName)
    println("Version code: " + variant.mergedFlavor.versionCode)


}

// -------------------------------------------------------------------------------------------------
// Generate blocklists
// -------------------------------------------------------------------------------------------------

def blockListOutputDir = 'src/main/res/raw'

task buildBlocklists(type:Copy) {
    from('../shavar-prod-lists') {
        include '*.json'
    }
    into blockListOutputDir

    // Android can't handle dashes in the filename, so we need to rename:
    rename 'disconnect-blacklist.json', 'blocklist.json'
    rename 'disconnect-entitylist.json', 'entitylist.json'
    // google_mapping.json already has an expected name
}

clean.doLast {
    delete fileTree(blockListOutputDir) {
        include '**/*.json'
    }
}

tasks.whenTaskAdded { task ->
    def name = task.name
    if (name.contains("generate") && name.contains("Config")) {
        task.dependsOn buildBlocklists
    }
}

// -------------------------------------------------------------------------------------------------
// Adjust: Read token from locale file if it exists (Only release builds)
// -------------------------------------------------------------------------------------------------

android.applicationVariants.all { variant ->
    def variantName = variant.getName()

    print("Adjust token: ")

    if (variantName.contains("Release") && variantName.contains("focus")) {
        try {
            def token = new File("${rootDir}/.adjust_token").text.trim()
            buildConfigField 'String', 'ADJUST_TOKEN', '"' + token + '"'
            println "(Added from .adjust_token file)"
        } catch (java.io.FileNotFoundException e) {
            buildConfigField 'String', 'ADJUST_TOKEN', 'null'
            println("X_X")
        }
    } else {
        buildConfigField 'String', 'ADJUST_TOKEN', 'null'
        println("--")
    }
}

// -------------------------------------------------------------------------------------------------
// Gecko: Put GeckoVersion into BuildConfig
// -------------------------------------------------------------------------------------------------

android.applicationVariants.all {
    buildConfigField 'String', 'GECKOVIEW_VERSION', "\"${geckoview_version}\""
}

// -------------------------------------------------------------------------------------------------
// Sentry: Read token from locale file if it exists (Only release builds)
// -------------------------------------------------------------------------------------------------

android.applicationVariants.all {
    print("Sentry token: ")
    try {
        def token = new File("${rootDir}/.sentry_token").text.trim()
        buildConfigField 'String', 'SENTRY_TOKEN', '"' + token + '"'
        println "(Added from .sentry_token file)"
    } catch (java.io.FileNotFoundException e) {
        buildConfigField 'String', 'SENTRY_TOKEN', 'null'
        println("X_X")
    }
}

// -------------------------------------------------------------------------------------------------
// L10N: Generate list of locales
// Focus provides its own (Android independent) locale switcher. That switcher requires a list
// of locale codes. We generate that list here to avoid having to manually maintain a list of locales:
// -------------------------------------------------------------------------------------------------

def getEnabledLocales() {
    def resDir = file('src/main/res')

    def potentialLanguageDirs = resDir.listFiles(new FilenameFilter() {
        @Override
        boolean accept(File dir, String name) {
            return name.startsWith("values-")
        }
    })

    def langs = potentialLanguageDirs.findAll {
        // Only select locales where strings.xml exists
        // Some locales might only contain e.g. sumo URLS in urls.xml, and should be skipped (see es vs es-ES/es-MX/etc)
        return  file(new File(it, "strings.xml")).exists()
    } .collect {
        // And reduce down to actual values-* names
        return it.name
    } .collect {
        return it.substring("values-".length())
    } .collect {
        if (it.length() > 3 && it.contains("-r")) {
            // Android resource dirs add an "r" prefix to the region - we need to strip that for java usage
            // Add 1 to have the index of the r, without the dash
            def regionPrefixPosition = it.indexOf("-r") + 1

            return it.substring(0, regionPrefixPosition) + it.substring(regionPrefixPosition + 1)
        } else {
            return it
        }
    }.collect {
        return '"' + it + '"'
    }

    // en-US is the default language (in "values") and therefore needs to be added separately
    langs << "\"en-US\""

    return langs.sort { it }
}

def generatedLocaleListDir = 'src/main/java/org/mozilla/focus/generated'
def generatedLocaleListFilename = 'LocaleList.java'

task generateLocaleList {
    doLast {
        def dir = file(generatedLocaleListDir)
        dir.mkdir()
        def localeList = file(new File(dir, generatedLocaleListFilename))

        localeList.delete()
        localeList.createNewFile()
        localeList << "package org.mozilla.focus.generated;" << "\n" << "\n"
        localeList << "import java.util.Arrays;" << "\n"
        localeList << "import java.util.Collections;" << "\n"
        localeList << "import java.util.List;" << "\n"
        localeList << "\n"
        localeList << "public class LocaleList {" << "\n"
        // findbugs doesn't like "public static final String[]", see http://findbugs.sourceforge.net/bugDescriptions.html#MS_MUTABLE_ARRAY
        localeList << "    public static final List<String> BUNDLED_LOCALES = Collections.unmodifiableList(Arrays.asList(new String[] { "
        localeList << getEnabledLocales().join(", ") + " }));" << "\n"
        localeList << "}" << "\n"
    }
}

tasks.whenTaskAdded { task ->
    if (name.contains("compile")) {
        task.dependsOn generateLocaleList
    }
}

clean.doLast {
    file(generatedLocaleListDir).deleteDir()
}

// -------------------------------------------------------------------------------------------------
// Static Analysis: findbugs and pmd
// -------------------------------------------------------------------------------------------------
spotbugs {
    ignoreFailures = false

    effort = "min"
    // This selects what level of bugs to report: low means low priority issues will be reported
    // (in addition to medium+high), which corresponds to warning about everything.
    // TODO: boost this to low once low priority issues are fixed.
    reportLevel = "medium"

    excludeFilter = new File("${project.rootDir}/quality/spotbugs-exclude.xml")
}

task spotbugs(type: SpotBugsTask, dependsOn: "assemble", group: 'verification') {
    classes = files("$projectDir.absolutePath/build/intermediates/classes")

    source = fileTree('src/main/java')
    classpath = files()

    // Only one report format is supported. Html is easier to read, so let's use that
    // (xml is the one that's enabled by default).
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    toolVersion = '5.5.2'
    ignoreFailures = true
    ruleSetFiles = files("${project.rootDir}/quality/pmd-rules.xml")
    ruleSets = []
}

task pmd(type: Pmd, group: 'verification') {
    source 'src'
    include '**/*.java'
    exclude('**/gen/**',
            '**/debug/**',
            '**/androidTest/**',
            '**/test**/**')

    reports {
        xml.enabled = false
        html.enabled = true
        html {
            destination file("$projectDir.absolutePath/build/reports/pmd/pmd.html")
        }
    }
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/quality/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

afterEvaluate {
    check.dependsOn 'spotbugs', 'pmd', 'checkstyle'
}

if (project.hasProperty("coverage")) {
    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
        doLast { jacocoTestReport.execute() }
    }

    jacocoAndroidUnitTestReport {
        csv.enabled false
        html.enabled true
        xml.enabled true
    }

    android {
        buildTypes {
            debug {
                testCoverageEnabled true
                applicationIdSuffix ".coverage"
            }
        }
    }
}