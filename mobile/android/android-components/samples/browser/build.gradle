/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion config.compileSdkVersion

    defaultConfig {
        applicationId "org.mozilla.samples.browser"
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "clearPackageData", "true"
        testInstrumentationRunnerArgument "listener", "com.squareup.leakcanary.FailTestOnLeakRunListener"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "engine", "abi"

    productFlavors {
        // GeckoView release channels
        geckoNightly {
            dimension "engine"
        }
        geckoBeta {
            dimension "engine"
        }
        geckoRelease {
            dimension "engine"
        }

        // WebView
        system {
            dimension "engine"
        }

        servo {
            dimension "engine"
        }

        // Processor architecture
        arm {
            dimension "abi"
        }
        x86 {
            dimension "abi"
        }
        aarch64 {
            dimension "abi"
        }
        universal {
            dimension "abi"
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == "release") {
            // This is a sample app that we are not releasing. Save some time and do not build
            // release versions.
            setIgnore(true)
        }

        def flavors = variant.flavors*.name.toString().toLowerCase()
        if (flavors.contains("system") && !flavors.contains("universal")) {
            setIgnore(true)
        }

        // Only allow "universal" flavors of "system", "geckoviewNightly" and "geckoBeta" engines
        if (flavors.contains("universal") && !(flavors.contains("system") || flavors.contains("geckonightly") || flavors.contains("geckobeta"))) {
            setIgnore(true)
        }

        // Ignore all non-universal GeckoView Nightly builds
        if (!flavors.contains("universal") && flavors.contains("geckonightly")) {
            setIgnore(true)
        }

        // Ignore all non-universal GeckoView Beta builds
        if (!flavors.contains("universal") && flavors.contains("geckobeta")) {
            setIgnore(true)
        }

        if (flavors.contains("servo") && !flavors.contains("arm") && !flavors.contains("x86")) {
            setIgnore(true)
        }
    }
}

configurations {
    geckoReleaseArmImplementation {}
    geckoReleaseX86Implementation {}
    geckoReleaseAarch64Implementation {}

    servoX86Implementation {}
    servoArmImplementation {}
}

dependencies {
    implementation project(':concept-awesomebar')
    implementation project(':concept-fetch')
    implementation project(':concept-engine')
    implementation project(':concept-tabstray')
    implementation project(':concept-toolbar')
    implementation project(':concept-storage')

    implementation project(':browser-awesomebar')
    implementation project(':browser-engine-system')
    implementation project(':browser-domains')
    implementation project(':browser-icons')
    implementation project(':browser-search')
    implementation project(':browser-session')
    implementation project(':browser-state')
    implementation project(':browser-tabstray')
    implementation project(':browser-toolbar')
    implementation project(':browser-menu')
    implementation project(':browser-storage-memory')

    implementation project(':lib-fetch-httpurlconnection')

    implementation project(':feature-awesomebar')
    implementation project(':feature-app-links')
    implementation project(':feature-contextmenu')
    implementation project(':feature-customtabs')
    implementation project(':feature-intent')
    implementation project(':feature-media')
    implementation project(':feature-readerview')
    implementation project(':feature-search')
    implementation project(':feature-session')
    implementation project(':feature-toolbar')
    implementation project(':feature-tabs')
    implementation project(':feature-prompts')
    implementation project(':feature-pwa')
    implementation project(':feature-findinpage')
    implementation project(':feature-sitepermissions')

    implementation project(':ui-autocomplete')

    implementation project(':support-utils')
    implementation project(':feature-downloads')
    implementation project(':support-ktx')

    servoImplementation project(':browser-engine-servo')
    servoX86Implementation Dependencies.mozilla_servo_x86
    servoArmImplementation Dependencies.mozilla_servo_arm

    geckoNightlyImplementation project(':browser-engine-gecko-nightly')

    geckoBetaImplementation project(':browser-engine-gecko-beta')

    geckoReleaseImplementation project(':browser-engine-gecko')
    geckoReleaseArmImplementation Gecko.geckoview_release_arm
    geckoReleaseX86Implementation Gecko.geckoview_release_x86
    geckoReleaseAarch64Implementation Gecko.geckoview_release_aarch64

    implementation Dependencies.google_material

    implementation Dependencies.kotlin_stdlib

    implementation Dependencies.androidx_appcompat
    implementation Dependencies.androidx_core_ktx
    implementation Dependencies.androidx_constraintlayout

    testImplementation project(':support-test')

    androidTestImplementation project(':support-android-test')
    androidTestImplementation Dependencies.androidx_test_core
    androidTestImplementation Dependencies.androidx_test_runner
    androidTestImplementation Dependencies.androidx_test_rules
    androidTestImplementation Dependencies.androidx_test_junit
    androidTestImplementation Dependencies.androidx_test_uiautomator
    androidTestImplementation Dependencies.androidx_espresso_core
    androidTestImplementation Dependencies.testing_mockwebserver
}
