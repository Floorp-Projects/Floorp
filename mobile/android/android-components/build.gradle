// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.geckoRelease = [
        revision: '84219fbf133cacfc6e31c9471ad20ee7162a02af',
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Deps.tools_androidgradle
        classpath Deps.tools_kotlingradle
        classpath Deps.tools_dokka

        // Publish.
        classpath Deps.tools_mavengradle
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC7-3")
}

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url "https://maven.mozilla.org/maven2"
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // GeckoView Release
        ////////////////////////////////////////////////////////////////////////////////////////////

        // ARM
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-release.revision.${geckoRelease['revision']}.mobile.android-api-16-opt/artifacts/public/android/maven"
        }

        // x86
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-release.revision.${geckoRelease['revision']}.mobile.android-x86-opt/artifacts/public/android/maven"
        }

        // aarch64
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-release.revision.${geckoRelease['revision']}.mobile.android-aarch64-opt/artifacts/public/android/maven"
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // Servo
        ////////////////////////////////////////////////////////////////////////////////////////////

        maven {
            url "https://download.servo.org/nightly/maven"
        }
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.dokka-android'

    afterEvaluate {
        if (it.hasProperty('android')) {
            jacoco {
                toolVersion = "0.8.2"
            }

            dependencies {
                lintChecks project(':tooling-lint')
            }

            android {
                testOptions {
                    unitTests {
                        includeAndroidResources = true
                    }
                }

                lintOptions {
                    warningsAsErrors true
                    abortOnError true
                }
            }

            if (project.hasProperty("coverage") && project.name != "support-test") {
                tasks.withType(Test) {
                    jacoco.includeNoLocationClasses = true
                    doLast { jacocoTestReport.execute() }
                }

                task jacocoTestReport(type: JacocoReport) {
                    reports {
                        xml.enabled = true
                        html.enabled = true
                    }

                    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                                      '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*']
                    def kotlinDebugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
                    def javaDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
                    def mainSrc = "$project.projectDir/src/main/java"

                    sourceDirectories = files([mainSrc])
                    classDirectories = files([kotlinDebugTree, javaDebugTree])
                    executionData = fileTree(dir: project.buildDir, includes: [
                            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
                    ])
                }

                android {
                    buildTypes {
                        debug {
                            testCoverageEnabled true
                        }
                    }
                }
            }

            if (it.hasProperty('kotlin')) {
                android {
                    kotlin {
                        experimental {
                            coroutines 'enable'
                        }
                    }
                }
            }
        }
    }
}

evaluationDependsOnChildren()

task docs(type: org.jetbrains.dokka.gradle.DokkaAndroidTask, overwrite: true) {
    moduleName = "$rootProject.name"
    outputDirectory = "$buildDir/javadoc"
    outputFormat = "html"
    jdkVersion = 7

    subprojects.findAll {
        it.hasProperty("android") && !it.name.startsWith("samples")
    }.each {
        classpath += files(it.android.getBootClasspath().join(File.pathSeparator))
        it.android.libraryVariants.all { variant ->
            classpath += files(variant.javaCompile.classpath.files)
        }
    }

    sourceDirs = files(subprojects.findAll {
        !it.name.startsWith("samples")
    }.collect { p ->
        def path = new File(p.projectDir, "/src/main/java")

        def relativePath = rootDir.toPath().relativize(path.toPath()).toString()
        linkMapping {
            dir = path
            url = "https://github.com/mozilla-mobile/android-components/blob/master/$relativePath"
            suffix = "#L"
        }

        return path
    })
}


task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.0.0.RC7-3"
    profile("main") {
        input = "$projectDir/components"
        config = "$projectDir/config/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt"
        baseline = "$projectDir/config/detekt-baseline.xml"
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.24.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "components/**/*.kt" , "samples/**/*.kt"
}

task printModules {
    doLast {
        subprojects.each { p ->
            println "module: " + p.path
        }
    }
}

task printVersion {
    doLast {
        println("version: " + Config.componentsVersion)
    }
}

apply plugin: GVNightlyVersionVerifierPlugin
apply plugin: GitHubPlugin