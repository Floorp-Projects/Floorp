// Top-level build file where you can add configuration options common to all sub-projects/modules.

import org.gradle.internal.logging.text.StyledTextOutput.Style
import org.gradle.internal.logging.text.StyledTextOutputFactory
import static org.gradle.api.tasks.testing.TestResult.ResultType

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath Dependencies.tools_androidgradle
        classpath Dependencies.tools_kotlingradle
        classpath Dependencies.tools_dokka
        classpath Dependencies.tools_androidmavenpublish
        // Mozilla Android gradle plugin. For details, see the repo:
        // https://github.com/mozilla-mobile/android-automation-tools/tree/master/gradle-plugin
        classpath "org.mozilla.android:gradle-plugin:0.2"
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0-RC11")
}

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url "https://maven.mozilla.org/maven2"
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // Servo
        ////////////////////////////////////////////////////////////////////////////////////////////

        maven {
            url "https://download.servo.org/nightly/maven"
        }
    }

    // Apply necessary Mozilla repositories as defined by the shared plugin.
    apply plugin: 'org.mozilla.android'
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.dokka-android'

    // Enable Kotlin warnings as errors. We should work to enable this on all modules (#2346).
    if (!KotlinCompiler.projectsWithWarningsAsErrorsDisabled.contains(project.name)) {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions.allWarningsAsErrors = true
        }
    }

    // Enforce that all (transitive) dependencies are using the same support library version as we do.
    project.configurations.all {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex') ) {
                details.useVersion Versions.support_libraries
            }
        }
    }

    afterEvaluate {
        if (it.hasProperty('android')) {
            jacoco {
                toolVersion = "0.8.3"
            }

            // Format test output
            tasks.matching {it instanceof Test}.all {
                systemProperty "robolectric.logging", "stdout"
                systemProperty "logging.test-mode", "true"

                testLogging.events = []

                def out = services.get(StyledTextOutputFactory).create("an-ouput")

                beforeSuite { descriptor ->
                    if (descriptor.getClassName() != null) {
                        out.style(Style.Header).println("\nSUITE: " + descriptor.getClassName())
                    }
                }

                beforeTest { descriptor ->
                    out.style(Style.Description).println("  TEST: " + descriptor.getName())
                }

                onOutput { descriptor, event ->
                    logger.lifecycle("    " + event.message.trim())
                }

                afterTest { descriptor, result ->
                    switch (result.getResultType()) {
                        case ResultType.SUCCESS:
                            out.style(Style.Success).println("  SUCCESS")
                            break

                        case ResultType.FAILURE:
                            out.style(Style.Failure).println("  FAILURE")
                            logger.lifecycle("", result.getException())
                            break

                        case ResultType.SKIPPED:
                            out.style(Style.Info).println("  SKIPPED")
                            break
                    }
                    logger.lifecycle("")
                }
            }

            dependencies {
                lintChecks project(':tooling-lint')
            }

            android {
                testOptions {
                    unitTests {
                        includeAndroidResources = true
                    }
                }

                lintOptions {
                    warningsAsErrors true
                    abortOnError (project.name != "support-test")
                }
            }

            if (project.hasProperty("coverage") && project.name != "support-test") {
                tasks.withType(Test) {
                    jacoco.includeNoLocationClasses = true
                    doLast { jacocoTestReport.execute() }
                }

                task jacocoTestReport(type: JacocoReport) {
                    reports {
                        xml.enabled = true
                        html.enabled = true
                    }

                    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                                      '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*']
                    def kotlinDebugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
                    def javaDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
                    def mainSrc = "$project.projectDir/src/main/java"

                    sourceDirectories = files([mainSrc])
                    classDirectories = files([kotlinDebugTree, javaDebugTree])
                    executionData = fileTree(dir: project.buildDir, includes: [
                            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
                    ])
                }

                android {
                    buildTypes {
                        debug {
                            testCoverageEnabled true
                        }
                    }
                }
            }
        }
    }
}

evaluationDependsOnChildren()

task docs(type: org.jetbrains.dokka.gradle.DokkaAndroidTask, overwrite: true) {
    moduleName = "$rootProject.name"
    outputDirectory = "$buildDir/javadoc"
    outputFormat = "gfm"
    jdkVersion = 7

    subprojects.findAll {
        it.hasProperty("android") && !it.name.startsWith("samples")
    }.each {
        classpath += files(it.android.getBootClasspath().join(File.pathSeparator))
        it.android.libraryVariants.all { variant ->
            classpath += files(variant.javaCompile.classpath.files)
        }
    }

    externalDocumentationLink {
        url = new URL("https://mozilla.github.io/geckoview/javadoc/mozilla-central/package-list")
    }

    sourceDirs = files(subprojects.findAll {
        !it.name.startsWith("samples")
    }.collect { p ->
        def path = new File(p.projectDir, "/src/main/java")

        def relativePath = rootDir.toPath().relativize(path.toPath()).toString()
        linkMapping {
            dir = path
            url = "https://github.com/mozilla-mobile/android-components/blob/master/$relativePath"
            suffix = "#L"
        }

        return path
    })
}


task clean(type: Delete) {
    delete rootProject.buildDir
}


detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.0.0-RC11"
    input = files("$projectDir/components", "$projectDir/buildSrc", "$projectDir/samples")
    config = files("$projectDir/config/detekt.yml")
    filters = ".*test.*,.*/resources/.*,.*/tmp/.*,.*/build/.*"
    baseline = file("$projectDir/config/detekt-baseline.xml")

    reports {
        html {
            enabled = true
            destination = file("$projectDir/build/reports/detekt.html")
        }
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.29.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "components/**/*.kt" , "samples/**/*.kt", "!**/build", "buildSrc/**/*.kt"
}

apply plugin: GVNightlyVersionVerifierPlugin
apply plugin: GitHubPlugin
