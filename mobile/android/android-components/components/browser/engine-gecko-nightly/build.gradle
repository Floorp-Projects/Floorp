/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.build['compileSdkVersion']

    defaultConfig {
        minSdkVersion rootProject.ext.build['minSdkVersion']
        targetSdkVersion rootProject.ext.build['targetSdkVersion']
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':concept-engine')

    // We only compile against the ARM artifact. We let the app decide which artifact to actually
    // use at runtime. As the Kotlin/Java API is the same for all ABIs it is not important which one
    // we import here.
    compileOnly "org.mozilla:geckoview-nightly-armeabi-v7a:${rootProject.ext.geckoNightly['version']}"
    testImplementation "org.mozilla:geckoview-nightly-armeabi-v7a:${rootProject.ext.geckoNightly['version']}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${rootProject.ext.dependencies['kotlin']}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.dependencies['coroutines']}"

    testImplementation project(':support-test')
    testImplementation "junit:junit:${rootProject.ext.dependencies['junit']}"
    testImplementation "org.robolectric:robolectric:${rootProject.ext.dependencies['robolectric']}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.dependencies['mockito']}"
    testImplementation "com.android.support:appcompat-v7:${rootProject.ext.dependencies['supportLibraries']}"
}

archivesBaseName = "engine-gecko-nightly"

apply from: '../../../publish.gradle'
ext.configurePublish(
        'org.mozilla.components',
        'engine-gecko-nightly',
        'Engine implementation based on GeckoView (Nightly).')
