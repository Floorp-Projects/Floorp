/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion config.compileSdkVersion

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.incremental": "true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    implementation project(':browser-state')
    implementation project(':support-ktx')
    implementation project(':support-base')

    implementation ComponentsDependencies.kotlin_stdlib
    implementation ComponentsDependencies.kotlin_coroutines

    implementation ComponentsDependencies.androidx_paging
    implementation ComponentsDependencies.androidx_lifecycle_livedata

    implementation ComponentsDependencies.androidx_room_runtime
    kapt ComponentsDependencies.androidx_room_compiler

    androidTestImplementation project(':support-android-test')

    androidTestImplementation ComponentsDependencies.androidx_room_testing
    androidTestImplementation ComponentsDependencies.androidx_arch_core_testing
    androidTestImplementation ComponentsDependencies.androidx_test_core
    androidTestImplementation ComponentsDependencies.androidx_test_runner
    androidTestImplementation ComponentsDependencies.androidx_test_rules
    androidTestImplementation ComponentsDependencies.testing_coroutines

    testImplementation project(':support-test')
    testImplementation project(':support-test-libstate')

    testImplementation ComponentsDependencies.androidx_test_junit
    testImplementation ComponentsDependencies.testing_coroutines
    testImplementation ComponentsDependencies.testing_mockito
    testImplementation ComponentsDependencies.testing_robolectric
}

apply from: '../../../android-lint.gradle'
apply from: '../../../publish.gradle'
ext.configurePublish(config.componentsGroupId, archivesBaseName, project.ext.description)
