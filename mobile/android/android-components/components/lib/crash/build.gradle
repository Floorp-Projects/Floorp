/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

buildscript {
    repositories {
        maven {
            url "https://maven.mozilla.org/maven2"
        }

        gradlePluginPortal()

        dependencies {
            classpath "org.mozilla.telemetry:glean-gradle-plugin:${Versions.mozilla_glean}"
            classpath ComponentsDependencies.plugin_ksp
        }
    }
}

plugins {
    id "com.jetbrains.python.envs" version "0.0.26"
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion config.compileSdkVersion

    defaultConfig {
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.incremental": "true"]
            }
        }
    }

    ksp {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    namespace 'mozilla.components.lib.crash'
}

dependencies {
    implementation ComponentsDependencies.kotlin_coroutines

    implementation ComponentsDependencies.androidx_appcompat
    implementation ComponentsDependencies.androidx_constraintlayout
    implementation ComponentsDependencies.androidx_recyclerview

    implementation project(':support-base')
    implementation project(':support-ktx')
    implementation project(':support-utils')

    implementation Dependencies.androidx_room_runtime
    ksp ComponentsDependencies.androidx_room_compiler

    // We only compile against GeckoView and Glean. It's up to the app to add those dependencies if it wants to
    // send crash reports to Socorro (GV).
    compileOnly project(":service-glean")
    testImplementation project(":service-glean")
    testImplementation ComponentsDependencies.androidx_work_testing

    testImplementation project(':support-test')
    testImplementation ComponentsDependencies.androidx_test_core
    testImplementation ComponentsDependencies.androidx_test_junit
    testImplementation ComponentsDependencies.testing_robolectric
    testImplementation ComponentsDependencies.testing_mockito
    testImplementation ComponentsDependencies.testing_coroutines
    testImplementation ComponentsDependencies.testing_mockwebserver
    testImplementation ComponentsDependencies.mozilla_glean_forUnitTests
}

ext.gleanGenerateMarkdownDocs = true
apply plugin: "org.mozilla.telemetry.glean-gradle-plugin"
apply from: '../../../android-lint.gradle'
apply from: '../../../publish.gradle'
ext.configurePublish(config.componentsGroupId, archivesBaseName, project.ext.description)
