/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

import org.yaml.snakeyaml.Yaml

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.yaml:snakeyaml:1.23'
    }
}

def setupProject(name, path, description, shouldPublish) {
    settings.include(":$name")

    project(":$name").projectDir = new File(rootDir, path)

    if (!gradle.ext.has('componentDescriptions')) {
        gradle.ext.set('componentDescriptions', [:])
    }

    // TODO instead of using global "componentDescriptions" map, attach description to "project.ext"
    gradle.ext.componentDescriptions[name] = description

    // project(...) gives us a skeleton project that we can't set ext.* on
    gradle.afterProject { project ->
        // However, the "afterProject" listener iterates over every project and gives us the actual project
        // So, once we filter for the project we care about, we can set whatever we want
        if (project.name == name) {
            project.ext.shouldPublish = shouldPublish
        }
    }
}

def yaml = new Yaml()
def buildconfig = yaml.load(new File(rootDir, '.buildconfig.yml').newInputStream())
buildconfig.projects.each { project ->
    setupProject(project.key, project.value.path, project.value.description, project.value.shouldPublish)
}

gradle.projectsLoaded { ->
    // Wait until root project is "loaded" before we set "config"
    // Note that since this is set on "rootProject.ext", it will be "in scope" during the evaluation of all projects'
    // gradle files. This means that they can just access "config.<value>", and it'll function properly
    gradle.rootProject.ext.config = new Config(buildconfig.componentsVersion)
}
//////////////////////////////////////////////////////////////////////////
// Local Development overrides
//////////////////////////////////////////////////////////////////////////

Properties localProperties = null;
String settingAppServicesPath = "substitutions.application-services.dir";

if (file('local.properties').canRead()) {
    localProperties = new Properties()
    localProperties.load(file('local.properties').newDataInputStream())
    logger.lifecycle('Local configuration: loaded local.properties')
} else {
    logger.lifecycle('Local configuration: absent local.properties; proceeding as normal.')
}

if (localProperties != null) {
    String appServicesLocalPath = localProperties.getProperty(settingAppServicesPath);

    if (appServicesLocalPath != null) {
        logger.lifecycle("Local configuration: substituting application-services modules from path: $appServicesLocalPath")

        includeBuild(appServicesLocalPath) {
            dependencySubstitution {
                substitute module('org.mozilla.fxaclient:fxaclient') with project(':fxa-client-library')
                substitute module('org.mozilla.sync15:logins') with project(':logins-library')
                substitute module('org.mozilla.places:places') with project(':places-library')
            }
        }

    } else {
        logger.lifecycle("Local configuration: application-services substitution path missing. Specify it via '$settingAppServicesPath' setting.")
    }
}