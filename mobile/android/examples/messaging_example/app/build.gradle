buildDir "${topobjdir}/gradle/build/mobile/android/examples/messaging_example"

apply plugin: 'com.android.application'

apply from: "${topsrcdir}/mobile/android/gradle/product_flavors.gradle"

android {
    compileSdkVersion project.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "org.mozilla.geckoview.example.messaging"
        targetSdkVersion project.ext.targetSdkVersion
        minSdkVersion project.ext.minSdkVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // By default the android plugins ignores folders that start with `_`, but
    // we need those in web extensions.
    // See also:
    //  - https://issuetracker.google.com/issues/36911326
    //  - https://stackoverflow.com/questions/9206117/how-to-workaround-autoomitting-fiiles-folders-starting-with-underscore-in
    aaptOptions {
        ignoreAssetsPattern  '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        noCompress 'ja'
    }

    project.configureProductFlavors.delegate = it
    project.configureProductFlavors()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-annotations:$support_library_version"
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    // Replace this with implementation "org.mozilla.geckoview:geckoview-${geckoviewChannel}:${geckoviewVersion}"
    implementation project(path: ':geckoview')
}
